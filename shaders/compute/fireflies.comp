#version 460 core

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

struct Particle {
    vec4 position;
    vec4 velocity;
    vec4 acceleration;
    vec4 color;
};

layout(set = 0, binding = 0) buffer Particles {
    Particle data[];
} particles;

layout(set = 0, binding = 1) uniform sampler2D heightMap;

layout(push_constant) uniform PushConstants {
    float timeElapsed;
    float deltaTime;
} pushConstants;

vec3 RandomAcceleration(float seed) {
    return normalize(vec3(
        mod(sin(seed) * 43758.5453, 2.0) - 1.0,
        mod(sin(seed + 1.0) * 43758.5453, 2.0) - 1.0,
        mod(sin(seed + 2.0) * 43758.5453, 2.0) - 1.0
    ));
}

void main() {
    uint index = gl_GlobalInvocationID.x;

    Particle particle = particles.data[index];
    particle.acceleration = vec4(RandomAcceleration(float(index) * pushConstants.timeElapsed), 0.0);
    particle.velocity += particle.acceleration * pushConstants.deltaTime;
    particle.velocity = normalize(particle.velocity);
    particle.position += particle.velocity * pushConstants.deltaTime * 0.1;
    particle.position.y = max(0.0, texture(heightMap, (particle.position.xz / 15.0 + 1.0) / 2.0).r) + 0.1;

    if (particle.position.x > 15.0) {
        particle.position.x = -15.0;
    }
    if (particle.position.x < -15.0) {
        particle.position.x = 15.0;
    }
    if (particle.position.z > 15.0) {
        particle.position.z = -15.0;
    }
    if (particle.position.z < -15.0) {
        particle.position.z = 15.0;
    }

    particles.data[index] = particle;
}