#version 460 core

layout(local_size_x = 32, local_size_y = 32) in;

layout (set = 0, binding = 0) uniform Settings {
    float maxHeight;
    uint lodCount;
    vec4 lodWeights[5];
} settings;

layout (set = 0, binding = 1) uniform sampler2D noiseTextures;
layout (set = 0, binding = 2) writeonly uniform image2D heightMap;
layout (set = 0, binding = 3) uniform sampler2DArray albedoTextures;
layout (set = 0, binding = 4) writeonly uniform image2D albedo;
layout (set = 0, binding = 5) uniform sampler2DArray normalTextures;
layout (set = 0, binding = 6) writeonly uniform image2D normal;

float SampleHeight(vec2 uv) {
    float d = length((uv - 0.5) * 2);
    float height = (1 + tanh(d * 12 - 4)) * settings.maxHeight / 2;

    for (int i = 0; i < settings.lodCount; i++) {
        height += (textureLod(noiseTextures, uv, i).r - 0.5f) * settings.maxHeight * settings.lodWeights[i / 4][i % 4] * 4;
    }

    return height;
}

vec3 GenerateNormal(vec2 uv) {
    float hL = SampleHeight(uv + vec2(1.0 / imageSize(heightMap).x, 0.0));
    float hR = SampleHeight(uv - vec2(1.0 / imageSize(heightMap).x, 0.0));
    float hU = SampleHeight(uv + vec2(0.0, 1.0 / imageSize(heightMap).y));
    float hD = SampleHeight(uv - vec2(0.0, 1.0 / imageSize(heightMap).y));

    return normalize(vec3(hL - hR, 2.0 / settings.maxHeight, hD - hU));
}

vec3 GenerateAlbedo(vec2 uv) {
    float height = SampleHeight(uv);
    vec3 N = GenerateNormal(uv);
    float slope = dot(N, vec3(0, 1, 0));

    vec2 sampleUV = uv * 2048;
    vec4 sand = texture(albedoTextures, vec3(sampleUV, 0));
    vec4 grass = texture(albedoTextures, vec3(sampleUV, 1));
    vec4 rock = texture(albedoTextures, vec3(sampleUV, 2));
    vec4 snow = texture(albedoTextures, vec3(sampleUV, 3));

    vec3 color = vec3(0);
    if (height < 0.1) {
        color = sand.rgb;
    } else if (height > 0.1 && height < 0.3) {
        color = mix(sand, grass, (height - 0.1) / 0.2).rgb;
    } else if (height > 0.3 && height < 6) {
        color = grass.rgb;
    } else if (height > 6 && height < 8) {
        color = mix(grass, snow, (height - 6) / 2).rgb;
    } else {
        color = snow.rgb;
    }
    if (slope < 0.5 && slope > 0.1) {
        color = mix(rock.rgb, color, (slope - 0.1) / 0.4);
    } else if (slope < 0.1) {
        color = rock.rgb;
    }
    return color;


}

void main() {
    ivec2 texel = ivec2(gl_GlobalInvocationID.xy);
    vec2 uv = vec2(texel) / vec2(imageSize(heightMap));

    float height = SampleHeight(uv);
    imageStore(heightMap, texel, vec4(height, 0.0, 0.0, 0.0));
    imageStore(albedo, texel, vec4(GenerateAlbedo(uv), 1.0));
    imageStore(normal, texel, vec4(GenerateNormal(uv), 1.0));
}