#version 460 core

layout(local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

layout(set = 0, binding = 0, rgba32f) readonly uniform image2D inputImage;

layout(set = 0, binding = 1, rgba32f) uniform image2D hash1;
layout(set = 0, binding = 2, rgba32f) uniform image2D hash2;
layout(set = 0, binding = 3, rgba32f) uniform image2D hash3;

layout(set = 0, binding = 4, rgba32f) uniform image2D hash;

layout(push_constant) uniform Data {
    vec4 hash1Data;
    vec4 hash2Data;
    vec4 hash3Data;
} data;


float grayscale(ivec2 pixel) {
    vec4 color = imageLoad(inputImage, pixel);
    return 0.2126 * color.r + 0.7152 * color.g + 0.0722 * color.b;
}

float hashFunc(float grayscaleValue, vec2 pixelPosition, float a, float b, float c, float tresh) {
    if (grayscaleValue > tresh) {
        return 1.0f;
    }

    vec2 line = vec2(a, b);

    return sin(dot(pixelPosition, line)) > c ? 1.0 : 0.0;
}

void main() {
    ivec2 pixelCoords = ivec2(gl_GlobalInvocationID.xy);
    ivec2 resolution = imageSize(inputImage);

    if (pixelCoords.x >= resolution.x || pixelCoords.y >= resolution.y) {
        return;
    }

    float grayscaleValue = grayscale(pixelCoords);

    float hash1Value = hashFunc(grayscaleValue, vec2(pixelCoords), data.hash1Data.x, data.hash1Data.y, data.hash1Data.z, data.hash1Data.w);
    imageStore(hash1, pixelCoords, vec4(hash1Value, hash1Value, hash1Value, 1.0));

    float hash2Value = hashFunc(grayscaleValue, vec2(pixelCoords), data.hash2Data.x, data.hash2Data.y, data.hash2Data.z, data.hash2Data.w);
    imageStore(hash2, pixelCoords, vec4(hash2Value, hash2Value, hash2Value, 1.0));

    float hash3Value = hashFunc(grayscaleValue, vec2(pixelCoords), data.hash3Data.x, data.hash3Data.y, data.hash3Data.z, data.hash3Data.w);
    imageStore(hash3, pixelCoords, vec4(hash3Value, hash3Value, hash3Value, 1.0));

    float hashValue = clamp(hash1Value + hash2Value + hash3Value, 0.0, 1.0);
    imageStore(hash, pixelCoords, vec4(hashValue, hashValue, hashValue, 1.0));
}